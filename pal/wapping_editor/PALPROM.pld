/* ***********************************************/
/* PALPROM design for Wapping Editor             */
/* ***********************************************/

Name     PALPROM     ;
PartNo   00          ;
Date     09/09/2023  ;
Revision 02          ;
Designer David Banks ;
Company  NA          ;
Assembly None        ;
Location             ;
Device   g16v8       ;

/* *************** INPUT PINS *********************/

PIN 1    = A12;
PIN 2    = A7;
PIN 3    = A13;
PIN 4    = A8;
PIN 5    = A9;
PIN 6    = A11;
PIN 7    = NOE;
PIN 8    = A10;
PIN 9    = NCE;
PIN 11   = A5;
PIN 18   = A6;

/* *************** OUTPUT PINS *********************/

PIN 12   = A13OUT;  /* A13 to 27C512 */
PIN 17   = A14OUT;  /* A14 to 27C512 */
PIN 19   = A15OUT;  /* A15 to 27C512 */

PIN 13   = B0;      /* Bank select latch bit 0,        not connected on PCB */
PIN 14   = B1;      /* Bank select latch bit 1,        not connected on PCB */
PIN 15   = B2;      /* Bank select latch bit 2,        not connected on PCB */
PIN 16   = WRITE;   /* Used to reduce # product terms, not connected on PCB */

/* *************** EQUATIONS ***********************/

/* &9F00 BANK 0 (replicates 1st 8k in second 8k of slot) */
/* &9F20 BANK 1 */
/* &9F40 BANK 2 */
/* &9F60 BANK 3 */
/* &9F80 BANK 4 */
/* &9FA0 BANK 5 */
/* &9FC0 BANK 6 */
/* &9FE0 BANK 7 */

WRITE = !NOE & !NCE & !A13 & A12 & A11 & A10 & A9 & A8;

/* B2/B1/B0 are D-type latches that represent the currently selected bank */

B0 = (B0 & !WRITE) # (A5 & WRITE);
B1 = (B1 & !WRITE) # (A6 & WRITE);
B2 = (B2 & !WRITE) # (A7 & WRITE);

/* If !A13 then always select BANK 0, otherwise select based on B2/B1/B0 */
A13OUT = A13 & B0;
A14OUT = A13 & B1;
A15OUT = A13 & B2;
